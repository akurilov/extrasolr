buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id "java"
    id "com.bmuschko.docker-remote-api" version "6.0.0"
}

repositories {
    mavenCentral()
}

dependencies {
    compile(
        "io.netty:netty-buffer:${nettyVersion}",
        "io.netty:netty-common:${nettyVersion}",
        "io.netty:netty-handler:${nettyVersion}",
        "io.netty:netty-transport:${nettyVersion}",
        "io.netty:netty-codec:${nettyVersion}",
        "io.netty:netty-resolver:${nettyVersion}",
        "io.netty:netty-transport-native-epoll:${nettyVersion}:linux-x86_64",
        "io.netty:netty-tcnative-boringssl-static:${nettyTcNativeVersion}",
        "io.netty:netty-codec-http:${nettyVersion}",
        "org.javassist:javassist:3.26.0-GA",
        "io.nats:jnats:${natsVersion}",
        "org.apache.solr:solr-solrj:${solrVersion}",
        "net.htmlparser.jericho:jericho-html:3.4",
        "org.apache.commons:commons-collections4:4.4",
        "org.slf4j:slf4j-simple:1.7.24",
        "com.squareup.okhttp3:okhttp:3.14.4",
    )
    testCompile("org.junit.jupiter:junit-jupiter:5.5.1")
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

tasks {
    wrapper {
        gradleVersion = "6.0"
    }
    jar {
        from configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    test {
        useJUnitPlatform()
        testLogging {
            events("passed", "skipped", "failed")
        }
    }
}

import com.bmuschko.gradle.docker.tasks.image.*

task dockerBuildImage(type: DockerBuildImage, dependsOn: jar) {
    inputDir = project.projectDir
    buildArgs = [
        "NATS_VERSION": natsVersion,
        "SOLR_VERSION": solrVersion,
    ]
    dockerFile = project.file("ci${File.separator}docker${File.separator}Dockerfile")
    imageId = "akurilov/${project.name}".toString()
}

task dockerTagImage(type: DockerTagImage, dependsOn: dockerBuildImage) {
    imageId = dockerBuildImage.imageId
    tag = "latest"
    repository = "akurilov/${project.name}"
}
